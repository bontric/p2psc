TITLE:: P2PSC
summary:: (Helper class to interact with p2psc node via OSC)
categories:: External Control
related:: Classes/NetAddr, Classes/OSCFunc

DESCRIPTION::
This is a helper class for interacting with a local p2psc node via OSC.
This class requires a p2psc node running (usually on your local machine).
See (https://github.com/bontric/p2psc) for more information regarding the p2psc node.

By default this class registers some OSC functions:

code::
		// say: Prints the meassage
		this.addPath({|msg| msg.postln}, "/say");

		// hush: Frees all OSCdefs
		this.addPath({|msg| this.resetPaths()}, "/hush");

		// load: Loads a file from given path
		this.addPath({|msg| PathName.new(msg[1].asString).asAbsolutePath.load}, "/load");

		// reset: Recompile class interpreter
		this.addPath({thisProcess.recompile()}, "/reset");
::

Note that this class communicates with an external application, which means that you need to manually make
sure, that client and server are in sync. You can call ".sync" on a p2psc instance after a call to make sure 
everything is done before moving on. (This is just a 0.1.wait for now, but a proper sync will be added in the future)

code::
(
	fork{
		p = P2PSC();
		p.sync;
		p.setName("Alice"); 
		p.sync;
	}
)
::

CLASSMETHODS::

METHOD:: new
Creates a new instance of P2PSC

ARGUMENT:: ip
IP address of the node as a string (Default:localhost)

ARGUMENT:: port
Port of the node (Default:3760)

returns:: A P2PSC object


INSTANCEMETHODS::
METHOD:: sync
    Wait for this instance to Synchronize with the server. Can only nbe called in a routine.

    Note: This function is currently just a wrapper and waits for 100ms. This will be fixed in the future

METHOD:: getPeers
    Get peer names as a list of strings. 

    Note: this function MUST be called within a routine, since it communicates with the p2psc node and waits for a response.

    returns:: A list of strings representing peer names currently available in the network.

METHOD:: getPaths
    Get all paths for the local p2psc node or a peer. 

    Note: this function MUST be called within a routine, since it communicates with the p2psc node and waits for a response.

    ARGUMENT:: peer
    If present, request this peers path's. Otherwise the local paths are returned.

    returns:: List of strings representing OSC paths subscribed by given peer.

METHOD:: addPath
Add a OSC path to the local and node registry.

    ARGUMENT:: function
    A function to call when a message for the given path is received. The arument for this function is a list of path and arguments. (So the first element of this list is the path and all following elements are the arguments)

    code::
    p = P2PSC();
    p.addPath({
        arg msg;
        var path = msg[0];
        var osc_arguments = msg.copyToEnd(1);
        path.postln;
        osc_arguments.postln;
    },"/test");
    ::


    ARGUMENT:: path
    OSC path

    ARGUMENT:: matching
    If matching is set to true, you can register a path containing simple regular expressions.
    For example you could set path to "/test*" and your function will match any path beginning with "/test".

    METHOD:: paths
    returns:: A Dictionary with paths as keys and associated OSCFunc as value


METHOD:: resetPaths
    Remove all paths registered by this instance of P2PSC.

METHOD:: removePath
    Remove given path. Deletes local OSCFunc and removes registry entry at local node.

    ARGUMENT:: path
    Path which should be removed

METHOD:: getGroups
    Get groups of the local node or a peer.

    Note: this function MUST be called within a routine, since it communicates with the p2psc node and waits for a response.

    ARGUMENT:: peer
    If peer is nil, the call returns the groups of the local node. Otherwise groups of the given peer (if it exists) are returned. 

    returns:: List of groups for local node or peer.

METHOD:: addGroup
    Add a group to your local p2psc node. All messsages sent to this group will also be sent to you (if you subscribe the path). 

    ARGUMENT:: group
    Group you want to add.

METHOD:: removeGroup
    Remove given group at local node.

    ARGUMENT:: group
    Group which should be removed

METHOD:: resetGroups
    Remove all groups set by this P2PSC instance from the local node.

METHOD:: groups
    A list of the local groups (All groups added locally by addGroup)

METHOD:: sendMsg
    Send a OSC message to the p2psc node.

    ARGUMENT:: path
    OSC path

    ARGUMENT::  ... args
    An arbitrary number of arguments which are added to the OSC message.

METHOD:: setName
    Set the local p2psc node's name

    ARGUMENT:: name
    Name which should be set

METHOD:: update
    Used internally to Synchronize information between p2psc node and P2PSC class instance.

METHOD:: resetName
    Reset the name to the name set in the p2psc node's config

METHOD:: name
    Your local node's name.

METHOD:: disconnect
    Disconnect from your local p2psc node

EXAMPLES::

This is an overview of the p2psc functions and how to use them.

code::
(
// Note: A p2psc instnance must be running on your local machine with the default port (3760) for this to work.
// See (https://github.com/bontric/p2psc) for more information 
{
	var paths,groups,oname;

	p = P2PSC();
	0.1.sleep();

	// Add Group
	p.addGroup("test");
	0.1.sleep();

	groups = p.getGroups();
	if (groups[1] != "test", {"ERROR: AddGroup failed!".postln; groups.postln});

	// Remove Group
	p.removeGroup("test");
	0.1.sleep();
	groups = p.getGroups();
	if (groups.size != 1, {"ERROR: RemoveGroup failed!".postln; groups.postln});

	// Add Path
	p.addPath({}, "/test");
	0.1.sleep();

	paths = p.getPaths();
	if (paths != ["/hush", "/load", "/reset", "/say", "/test"], {"ERROR: addPath failed!".postln; paths.postln});

	// Remove Path
	p.removePath("/test");
	0.1.sleep();
	paths = p.getPaths();
	if (paths != ["/hush", "/load", "/reset", "/say"], {"ERROR: RemovePaths failed!".postln; paths.postln });

	// Set Name
	if(p.name == nil,{ "ERROR: Name not initialized".postln});

	oname = p.name;
	p.setName("blabla");
	0.1.sleep();
	if(p.name != "blabla",{ "ERROR: Name not updated".postln; p.name});

    //reset name
	p.resetName();
	0.1.sleep();
	if(p.name != oname,{ "ERROR: Name not reset".postln; p.name});

	// Get Peers
	p.getPeers().postln;

    // Reset/Disconnect
	p.resetGroups();
	p.resetPaths();
	p.resetName();

	p.disconnect();
	"Done!".postln;

}.fork;
)
::

And here a more advanced example, where multiple nodes send a message to each other (one after another).

NOTE that "One Node" and "Another Node" need to be run on different machines, each running their own p2psc instance!
code::
// One Node

(
p = P2PSC();
p.setName("Alice"); // set your name accordingly

p.addPath({ |msg|
	var sleeptime = 1;
	var freq = 200; // Change this for every node
	msg.postln; //print message for debugging
	
	{SinOsc.ar(freq:freq)*0.5*EnvGen.kr(Env.perc(releaseTime:sleeptime-0.01), doneAction:2)}.play;
	fork {
		var nextpeer;
		var source_peer = msg[1].asString;
		var peers = p.getPeers();
		
		sleeptime.wait; // wait for one second
		
		// send to the next peer in our list 
		nextpeer = peers.wrapAt(1+peers.indexOfEqual(source_peer));
		
		p.sendMsg("/"++nextpeer++"/ping", p.name)
	};
},"/ping"
);
)


// Another Node
(
q = P2PSC();
q.setName("Bob"); // set your name accordingly

q.addPath({ |msg|
	var sleeptime = 1;
	var freq = 300; // Change this for every node
	msg.postln; //print message for debugging
	
	{SinOsc.ar(freq:freq)*0.5*EnvGen.kr(Env.perc(releaseTime:sleeptime-0.01), doneAction:2)}.play;
	fork {
		var nextpeer;
		var source_peer = msg[1].asString;
		var peers = q.getPeers();
		
		sleeptime.wait; // wait for one second
		
		// send to the next peer in our list 
		nextpeer = peers.wrapAt(1+peers.indexOfEqual(source_peer));

		q.sendMsg("/"++nextpeer++"/ping", q.name)
	};
},"/ping"
)
)


// Use this to start sending OSC messages
(
// We include the sender's name to form a circle by sending it to the next peer
// in our (alphabetically sorted) peer list.
fork {
	var peer, peers;
	peers = p.getPeers();
	if (peers.size > 0,
		{
			peer = peers[0];
			("Sending initial ping to:"+peer).postln;
			("/"++peer++"/ping").postln;
			p.sendMsg("/"++peer++"/ping" , p.name)
		},
		{"Error: No other peers in the network"}
	)
}
)
::